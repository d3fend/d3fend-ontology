PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX d3f: <http://d3fend.mitre.org/ontologies/d3fend.owl#>

SELECT DISTINCT ?entity ?property ?value

WHERE {

    # Select techniques where:
    #   Ignore top level classes
    #   Ignore parent classes who have DA relationships defined on their children

    ?entity rdfs:subClassOf* d3f:OffensiveTechnique .
    OPTIONAL{ ?entity rdfs:label ?property  }
    OPTIONAL{ ?entity d3f:attack-id ?value  }
    OPTIONAL{ ?entity atrm:atrm-id ?value  }
    OPTIONAL{ ?entity tmfk:tmfk-id ?value  }


    FILTER NOT EXISTS {
        ?entity ?p ?da .
        ?da rdfs:subClassOf* d3f:Artifact .
        ?p rdfs:subPropertyOf* d3f:may-be-associated-with .
    }

    # Filter the toplevel classes
    FILTER NOT EXISTS {
        ?entity rdfs:subClassOf d3f:OffensiveTechnique .
    }

    # Filter the parents whose children have DAs defined.
    FILTER NOT EXISTS {
        ?subt rdfs:subClassOf* ?entity .
        ?subt ?sp ?sda .
        ?sda rdfs:subClassOf* d3f:Artifact .
        ?sp rdfs:subPropertyOf* d3f:may-be-associated-with .
    }

    # Filter the children whose parents have DAs defined.
	FILTER NOT EXISTS {
      ?pentity ?pp ?pda .
      ?pda rdfs:subClassOf* d3f:Artifact .
      ?pp rdfs:subPropertyOf* d3f:may-be-associated-with .
      ?entity rdfs:subClassOf* ?pentity .
    }

} ORDER BY ?value
