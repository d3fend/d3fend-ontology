PREFIX ocsf: <https://schema.ocsf.io/1.1.0-dev/ontology/>
PREFIX d3f: <http://d3fend.mitre.org/ontologies/d3fend.owl#>
PREFIX rdf: <http://www.w3.org/1999/02/22-rdf-syntax-ns#>
PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX xsd: <http://www.w3.org/2001/XMLSchema#>
PREFIX jsonschema: <https://www.w3.org/2019/wot/json-schema#>
PREFIX skos: <http://www.w3.org/2004/02/skos/core#>
PREFIX dcterms: <http://purl.org/dc/terms/>
PREFIX iter: <http://w3id.org/sparql-generate/iter/>
PREFIX fun: <http://w3id.org/sparql-generate/fn/>

# The OCSF Attribute Dictionary defines attributes and includes references to the events and objects in which they are used. 

GENERATE <https://schema.ocsf.io/1.1.0-dev/dictionary> () {  
  # Generate the data types defined by the dictionary
  GENERATE {
    GENERATE <https://schema.ocsf.io/1.1.0-dev/type> ( ?type, ?typeIRI, ?typeKey ) .
  } ITERATOR iter:JSONListKeys ( ?types ) AS ?typeKey 
  WHERE {
    BIND( IRI(?versionIRI + ?typeKey) AS ?typeIRI )
    BIND( fun:JSONPath( ?types, "$.['{ ?typeKey }']") AS ?type )
  } .

  # Generate the attributes defined by the dictionary
  GENERATE {
    GENERATE <https://schema.ocsf.io/1.1.0-dev/attribute> ( ?attribute, ?attributeIRI, ?attributeKey ) .
  } ITERATOR iter:JSONListKeys ( ?attributes ) AS ?attributeKey 
  WHERE {
    BIND( IRI(?versionIRI + ?attributeKey) AS ?attributeIRI )
    BIND( fun:JSONPath( ?attributes, "$.['{ ?attributeKey }']") AS ?attribute )
  } .
} 

ITERATOR iter:JSONPath ( <https://github.com/ocsf/ocsf-schema/raw/main/dictionary.json> , "$", "$.attributes", "$.types.attributes" ) 
AS ?source ?attributes ?types

WHERE {
  BIND("https://schema.ocsf.io/1.1.0-dev/ontology/" AS ?versionIRI)
}
